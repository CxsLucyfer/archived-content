<p>A XULRunner application has no installer by default; however, these instructions can help you to easily create a Windows installer with <a class="external" href="http://www.jrsoftware.org/isinfo.php">Inno Setup</a>. The tutorial here uses the My App example from the article <a href="/en/Getting_started_with_XULRunner" title="en/Getting started with XULRunner">Getting started with XULRunner</a>.</p>

<h2 id="Step_1_Installing_Inno_Setup">Step 1: Installing Inno Setup</h2>

<p>Download Inno Setup from <a class="external" href="http://www.jrsoftware.org/isdl.php">Inno Setup Downloads</a>. If you only want to compile the installer, install the stable release. If you want to edit scripts, you should get the QuickStart Pack (which includes ISTool, the script editor. However, if you don't get the QuickStart Pack, you can later get ISTool from <a class="external" href="http://www.jrsoftware.org/is3rdparty.php">Inno Setup Third-Party Files</a>.</p>

<h2 id="Step_2_Creating_the_application">Step 2: Creating the application</h2>

<p>To make your application standalone, deploy your application according to the <a href="/en/XULRunner/Deploying_XULRunner_1.8" title="en/XULRunner/Deploying_XULRunner_1.8">Deploying XULRunner 1.8</a> article. My App's example follows.</p>

<ul>
 <li><code><var>myapp</var>/</code>

  <ul>
   <li><code>application.ini</code></li>
   <li><code>chrome/</code></li>
   <li><code>default/</code></li>
   <li><code><var>myapp</var>.exe</code> (copy <code>xulrunner-stub.exe</code> and rename)</li>
   <li><code>xulrunner/</code></li>
  </ul>
 </li>
</ul>

<p>Please test your application before deploying it. If your application does not run, neither will the installed version.</p>

<h2 id="Step_3_Creating_the_Inno_Setup_script">Step 3: Creating the Inno Setup script</h2>

<p>Create an Inno Setup Script (.iss file). The Inno Setup Script Wizard can't set chrome directory's deployment point, so you have to edit the iss file yourself. Also you had better set the <code>AppName</code> and <code>AppPublisher</code> properties to the same values specified in your XULRunner application's <code>application.ini</code> file.</p>

<p>My App's .iss file sample as follows:</p>

<pre>; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=My App
AppVerName=My App 1.0
AppPublisher=Mark Finkle
AppPublisherURL=http://developer.mozilla.org/en/docs/Getting_started_with_XULRunner
AppSupportURL=http://developer.mozilla.org/en/docs/Getting_started_with_XULRunner
AppUpdatesURL=http://developer.mozilla.org/en/docs/Getting_started_with_XULRunner
DefaultDirName={pf}\My App
DefaultGroupName=My App
AllowNoIcons=yes
OutputDir=..\build\output
OutputBaseFilename=myapp-1.0-win32
; SetupIconFile=
Compression=lzma
SolidCompression=yes

[Languages]
Name: english; MessagesFile: compiler:Default.isl

[Components]
Name: main; Description: My App; Types: full compact custom; Flags: fixed
Name: runtime; Description: XUL Runner Runtime; Types: full custom

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked
Name: quicklaunchicon; Description: {cm:CreateQuickLaunchIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked

[Files]
Source: C:\develop\XULRunnerInstaller\myapp\myapp.exe; DestDir: {app}; Components: main; Flags: ignoreversion
Source: C:\develop\XULRunnerInstaller\myapp\application.ini; DestDir: {app}; Components: main; Flags: ignoreversion
Source: C:\develop\XULRunnerInstaller\myapp\chrome\*; Excludes: .svn; DestDir: {app}\chrome; Components: main; Flags: ignoreversion recursesubdirs createallsubdirs
Source: C:\develop\XULRunnerInstaller\myapp\defaults\*; Excludes: .svn; DestDir: {app}\defaults; Components: main; Flags: ignoreversion recursesubdirs createallsubdirs
Source: C:\develop\XULRunnerInstaller\myapp\xulrunner\*; DestDir: {app}\xulrunner; Components: runtime; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: {group}\My App; Filename: {app}\myapp.exe
Name: {group}\{cm:UninstallProgram,XUL Explorer}; Filename: {uninstallexe}
Name: {userdesktop}\My App; Filename: {app}\myapp.exe; Tasks: desktopicon
Name: {userappdata}\Microsoft\Internet Explorer\Quick Launch\My App; Filename: {app}\myapp.exe; Tasks: quicklaunchicon

[Run]
Filename: {app}\myapp.exe; Description: {cm:LaunchProgram,My App}; OnlyBelowVersion: 0,6; Flags: nowait postinstall skipifsilent

</pre>

<h2 id="Step_4_Building_an_installer">Step 4: Building an installer</h2>

<p>Once your script is ready, start the Inno Setup Compiler, load the script file, and compile it. This will create the compiler, which you need to test by actually trying to install your XULRunner application. Don't forget to then test the installed application to be sure the installation did indeed work correctly, since if you accidentally omitted resources from the script, they'll be missing from the installed package. If your application starts up successfully, your installer worked correctly.</p>